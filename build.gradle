plugins {
	id 'fabric-loom' version '0.10-SNAPSHOT'
	id 'maven-publish'
	id "io.github.juuxel.loom-quiltflower-mini" version "1.1.0"
}

repositories {
	maven {
		name = "JitPack"
		url = "https://jitpack.io"
	}
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/"
	}
}

loom {
    setAccessWidenerPath(file("src/main/resources/hudtweaks.accesswidener"))
}

quiltflower {
	addToRuntimeClasspath.set(true)
}

group = project.maven_group
version = project.mod_version

allprojects {
	apply plugin: 'fabric-loom'

	tasks.withType(JavaCompile).configureEach {
		// ensure that the encoding is set to UTF-8, no matter what the system default is
		// this fixes some edge cases with special characters not displaying correctly
		// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
		// If Javadoc is generated, this must be specified in that task too.
		it.options.encoding = "UTF-8"

		// Minecraft 1.17 (21w19a) upwards uses Java 16.
		it.options.release = 16
	}
	
	archivesBaseName = project.name
	
	dependencies {
		minecraft "com.mojang:minecraft:${project.minecraft_version}"
		mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
		modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	}

	configurations {
		dev
	}

	jar {
		archiveClassifier.set "dev"
	}

	loom {
		setShareRemapCaches(true)
	}
	
	java {
		withSourcesJar()
	}

	jar {
		from "${rootDir}/LICENSE.txt"
	}

	afterEvaluate {
		processResources {
			inputs.property "version", project.version

			filesMatching("fabric.mod.json") {
				expand "version": project.version
			}
		}
	}
}

dependencies {
	modCompileOnly "com.terraformersmc:modmenu:${project.modmenu_version}"
	modRuntimeOnly "com.terraformersmc:modmenu:${project.modmenu_version}"

	afterEvaluate {
		implementation project(":HUDTweaks-apihelper")
		include project(":HUDTweaks-apihelper")
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
	
	// Select the repositories you want to publish to
	// To publish to maven local, no extra repositories are necessary. Just use the task `publishToMavenLocal`.
	repositories {
		// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	}
}
